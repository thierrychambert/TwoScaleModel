
  model{
    
    ## National Scale
    # Prior on lambda (population growth rate)
    mu.lam_0 ~ dunif(0, 10)

    # Prior on N0
    N[1] ~ dunif(0, max.N0)

    # Process model over time (population growth)
    for (t in 1:(T-1)){
      lambda[t] <- mu.lam_0
      N[t+1] <- N[t] * lambda[t]
    } # t
    
    
    ## Colony-groups Scale
    # Priors
    for(i in 1:I){
      b0[i] ~ dunif(0,100)
      b1[i] ~ dunif(0,100)
    } # i
      
    for(t in 1:T){
      for(i in 1:I){
        w[i,t] <- b0[i] + b1[i]*(t-1)
        gamma[i,t] <- w[i,t]/sum(w[1:I,t])
      } # i
    } # t
    
    # Process model : repartition of N among colony-groups 
    for(t in 1:T){
      NN[t] <- round(N[t])
      nb[1:I,t] <- gamma[1:I,t]*NN[t]
      n[1:I,t] <- nb[1:I,t]
    } # t
    
    ## Observation process
    for(i in 1:I){
      for(t in 1:T){
        y[i,t] ~ dpois(n[i,t]*PI[i,t])
      } # t
    } # i
    
  } # model
